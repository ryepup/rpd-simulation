(in-package :rpd-simulation-examples)

(defactor brian-braincell (spatial)
  ((state :accessor state :initarg :state))
  (:function self
   (let ((state (state self)))
     (setf (state self)
	   (cond ((eq :on state) :dying)
		 ((eq :dying state) :off)
		 ((eq 2 (length (look-around
				 self (lambda (c) (eq :on (state c)))))) :on)
		 (T :off))))))

(defun make-brians-brain ()
  (let ((sim (make-simulation :board (rpd-simulation::make-board 90 90))))
    (rpd-simulation::do-board (sim location)
      (rpd-simulation:activate sim
		(make-instance 'brian-braincell
			       :location location
			       :state (random-elt '(:on :off)))))
    sim))

(defun brains-brain ()
  (rpd-simulation::simulate (make-brians-brain)
   :stop-if
   (lambda (processes)
     (every (lambda (p)
	      (eq :off (state p)))
	    processes))))

(defun brains-brain-sdl ()
  (sdl:with-init ()
    (sdl:window 540 540)
    (setf (sdl:frame-rate) 0)
    (let (dying-surf alive-surf)
      (flet ((sdl-cell (color)
	       (let ((surface (sdl:create-surface 6 6)))
		 (sdl:fill-surface color :surface surface)
		 surface))
	     (render ()
	       (sdl:clear-display sdl:*black*)
	       (do-processes (p)
		 (unless (eq (state p) :off)
		   (let ((y (y p))
			 (x (x p))
			 (surf (if (eq (state p) :dying)
				   dying-surf alive-surf)))
		     (sdl:set-point-* surf
				      :x (1+ (* 6 x))
				      :y (1+ (* 6 y)))
		     (sdl:blit-surface surf))))
	       (sdl:update-display)))
	(setf dying-surf (sdl-cell sdl:*red*)
	      alive-surf (sdl-cell sdl:*white*))
	(let ((*simulation-step-hook* #'render))
	  (sdl:with-events ()
	    (:quit-event () t)
	    (:video-expose-event () (sdl:update-display))
	    (:idle () 
		   (brains-brain)
		   (sdl:push-quit-event)))
	  (sdl:free dying-surf)
	  (sdl:free alive-surf))))))
